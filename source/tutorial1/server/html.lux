##  Copyright (c) Eduardo Julian. All rights reserved.
##  This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
##  If a copy of the MPL was not distributed with this file,
##  You can obtain one at http://mozilla.org/MPL/2.0/.

(;import lux
         (lux (data [text "Text/" Text/Monoid]
                    text/format
                    [list "" List/Functor List/Fold])
              [meta]
              (meta syntax
                    [ast])))

## [Types]
(type: #export Html Text)

(type: #export Attributes
  (List [Text Text]))

## [Syntax]
(def: attributes^
  (Syntax (List [Text AST]))
  (record^ (*^ (&^ text^ id^))))

(syntax: #export (@attrs [attrs attributes^])
  (wrap (list (` (: Attributes
                    (list (~@ (map (: (-> [Text AST] AST)
                                      (lambda [[key val]]
                                        (` [(~ (ast;text key)) (~ val)])))
                                   attrs))))))))

(syntax: #export (%tag% [name local-symbol^])
  (wrap (list (ast;text name))))

(syntax: #export (%tag-func-name% [name local-symbol^])
  (wrap (list (ast;symbol ["" (Text/++ name "'")]))))

## [Functions]
(def: (attrs->text attrs)
  (-> Attributes Text)
  (|> attrs
      (map (lambda [[key val]] (<> key "=" "\"" val "\"")))
      (text;join-with " ")))

(def: #export (node name attrs children)
  (-> Text Attributes (List Html) Html)
  (<> "<" name " " (attrs->text attrs) ">" (text;join-with " " children) "</" name ">"))

(do-template [<name>]
  [(let% [<fun-name> (%tag-func-name% <name>)]
     (def: #export <fun-name>
       (node (%tag% <name>)))

     (syntax: #export (<name> attrs [children (*^ id^)])
       (let [attrs' (case attrs
                      [_ (#;RecordS _)]
                      (` (@attrs (~ attrs)))

                      _
                      attrs)]
         (wrap (list (` (<fun-name> (~ attrs') (list (~@ children)))))))))]

  ## Head
  [head]
  [meta]
  [link]
  [title]
  ## Body
  [body]
  [div]
  [span]
  [a]
  [form]
  [input]
  )
